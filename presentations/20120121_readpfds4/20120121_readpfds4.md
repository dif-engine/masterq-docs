# readpfds 4章をOCamlに翻訳
![background](debian.png)

Kiwamu Okabe

# PFDSはStandard MLで書かれてる

* OCamlの方がメジャーでは...
* $-notation使える処理系あるの？
* fun lazyキーワードもないような...
* このままだと実行できない...

ということで4章をOCamlに翻訳します。

# $-notationはlazyキーワード

$-notationはOCamlにはありません。

lazyキーワードを使うと、
lazy_t型で包めます。

~~~ {.text}
(* hogeは int lazy_t 型 *)
let hoge = lazy 1
~~~

# でもlazyは結合が強め...

~~~ {.text}
$ cat test_ocaml.ml
let hoge = lazy 1 + 2
$ omake
*** omake: reading OMakefiles
*** omake: finished reading OMakefiles (0.01 sec)
- build . test_ocaml.cmi
+ ocamlfind ocamlopt -package oUnit -warn-error A -annot \
 -I . -c test_ocaml.ml
File "test_ocaml.ml", line 1, characters 11-17:
Error: This expression has type int lazy_t
       but an expression was expected of type int
~~~

あーありゃりゃ。↓に解釈されちゃいました。

~~~ {.text}
let hoge = (lazy 1) + 2
~~~

# lazyの後は全部カッコでくるむ

~~~ {.text}
(* hogeは int lazy_t 型 *)
let hoge = lazy (1 + 2)
~~~

気持ちよく全部くるむところがポイントです。

~~~ {.text}
let rec map f s = lazy (match s with
  | (lazy SSnil) -> SSnil
  | (lazy (SScons(x, s))) -> SScons(f x, map f s))
~~~

# lazy_tの中を取り出す

Lazy.force を使うと取り出せます。

Lazy.force は "'a lazy_t -> 'a" な関数です。

~~~ {.text}
$ cat test_ocaml.ml
let hoge = lazy (1 + 2)

let _ = print_int (Lazy.force hoge)
(* (Lazy.force hoge) は Int 型 *)
$ ./test_ocaml
3
~~~

# lazyパターンってのでも外せます

~~~ {.text}
$ cat test_ocaml.ml
let hoge = lazy (1 + 2)

let _ = match hoge with
  | lazy i -> print_int i
$ ./test_ocaml
3
~~~

# 本: fun plus ($m, $n) = $m+n

これはOCamlだと

~~~ {.text}
let plus (lazy m) (lazy n) = lazy (m + n)
~~~

となります。
使い方はこんなん

~~~ {.text}
let _ = let i = Lazy.force (plus (lazy 1) (lazy 2)) in
	print_int i
~~~

# 本: fun lazy plus ($m, $n) = ...

これOCamlだと無理なんです。

~~~ {.text}
fun lazy plus ($m, $n) = $m+n
~~~

は

~~~ {.text}
fun plus (x, y) = $case (x, y) of ($m, $n) => force ($m+n)
~~~

なので、OCamlではmatchを使うしかない。

~~~ {.text}
(* plus : int lazy_t -> int lazy_t -> int lazy_t *)
let plus m n = lazy(match (m, n) with
  | (lazy m, lazy n) -> m + n)
~~~

# 準備できたのでStreamを作ろう

PFDSの4.2章でStreamを作っているので、

OCamlでも作りましょう。

要はHaskellの遅延リストです。

# Streamの型

~~~ {.text}
datatype a StreamCell = NIL | CONS of a x a Stream
withtype a Stream = a StreamCell susp
~~~

はOCamlでは↓

~~~ {.text}
type 'a cell = SSnil | SScons of 'a * 'a stream
and 'a stream = 'a cell Lazy.t
~~~

# (++)

~~~ {.text}
fun lazy ($NIL) ++ t = t
       | ($CONS (x, s)) ++ t = $CONS (x, s ++ t)
~~~

はOCamlでは↓

~~~ {.text}
let rec (++) t1 t2 = lazy (match (t1, t2) with
  | (lazy SSnil, lazy t2) -> t2
  | (lazy (SScons(x, s)), t2) -> SScons(x, s ++ t2))
~~~

# take

~~~ {.text}
fun lazy take (0, s) = $NIL
       | take (n, $NIL) = $NIL
       | take (n, $CONS (x, s)) = $CONS (x, take (n - 1, s))
~~~

はOCamlでは↓

~~~ {.text}
let rec take n s = lazy (match (n, s) with
  | (0, _) -> SSnil
  | (_, lazy SSnil) -> SSnil
  | (n, lazy (SScons(x, s))) -> SScons(x, take (n - 1) s))
~~~

# drop

~~~ {.text}
fun lazy drop (n, s) =
  let fun drop' (0, s) = s
        | drop' (n, $NIL) = $NIL
        | drop' (n, $CONS (x, s)) = drop' (n - 1, s)
  in drop' (n, s) end
~~~

はOCamlでは↓

~~~ {.text}
let drop n s = lazy (
  let rec drop' n s = match (n, s) with
    | (0, lazy s) -> s
    | (_, lazy SSnil) -> SSnil
    | (n, lazy (SScons(_, s))) -> drop' (n - 1) s
  in drop' n s)
~~~

# reverse

~~~ {.text}
fun lazy reverse s =
  let fun reverse' ($NIL, r) = r
        | reverse' ($CONS (x, s), r) = reverse' (s, $CONS (x, r))
  in reverse' (s, $NIL) end
~~~

はOCamlでは↓

~~~ {.text}
let reverse s = lazy (
  let rec reverse' s r = match (s, r) with
    | (lazy SSnil, r) -> r
    | (lazy (SScons(x, s)), r) -> reverse' s (SScons(x, lazy r))
  in reverse' s SSnil)
~~~

# おまけ

Haskellの遅延リストをOCamlに移植したよ。

~~~ {.text}
http://github.com/master-q/\
readPurelyFunctionalDataStructures/LazyEvaluation
~~~

foldrとかzipWithとかunfoldrとかあるよ。
